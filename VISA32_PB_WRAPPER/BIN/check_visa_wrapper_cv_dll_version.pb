  Procedure.s GetFileVersionString(FileSpec.s)
    dummy.l
    BufferLen.l
   
    VerBuffer.VS_FIXEDFILEINFO
    VerPointer.l
    VerBufferLen.l
   
    FileVersion.s
   
    If OpenLibrary(0, "version.dll")
      *GFI_Size = GetFunction(0, "GetFileVersionInfoSizeA")
      *GFI_Info = GetFunction(0, "GetFileVersionInfoA")
      *GFI_Value = GetFunction(0, "VerQueryValueA")
     
      If *GFI_Size And *GFI_Info And *GFI_Value
        ; get size of version information buffer
        BufferLen = CallFunctionFast(*GFI_Size, @FileSpec, @dummy)
        If BufferLen
          ; allocate buffer and get version information
          *Buffer = AllocateMemory(BufferLen)
          CallFunctionFast(*GFI_Info, @FileSpec, 0, BufferLen, *Buffer)
          ; find file version and copy into structure
          CallFunctionFast(*GFI_Value, *Buffer, @"\", @VerPointer, @VerBufferLen)
          CopyMemory(VerPointer, VerBuffer, VerBufferLen)
          ; build file version string "x.x.x.x"
          FileVersion = Str((VerBuffer\dwFileVersionMS & $FFFF0000) >> 16) + "." + Str(VerBuffer\dwFileVersionMS & $0000FFFF) + "." + Str((VerBuffer\dwFileVersionLS & $FFFF0000) >> 16) + "." + Str(VerBuffer\dwFileVersionLS & $0000FFFF)
        EndIf
      EndIf
      CloseLibrary(0)
    EndIf   
    ; return file version string
    ProcedureReturn FileVersion
  EndProcedure
  
  
  current.s{#MAX_PATH} = GetCurrentDirectory()
  
  wrapper_current.s{#MAX_PATH} = current  + "VISA32_PB_WRAPPER.dll"
  Select  FileSize(wrapper_current)
    Case  -1, -2, 0
      MessageBox_(GetFocus_(), "no VISA32_PB_WRAPPER.dll found in " + current, "cv", #MB_ICONERROR|#MB_SYSTEMMODAL)
      End
    Default
      ;
  EndSelect
  
  WSysName.s = Space(#MAX_PATH)
  GetSystemDirectory_(WSysName, @Null)
  WSysName  = WSysName  + "\"
  
  wrapper_sys.s{1024} = WSysName  + "VISA32_PB_WRAPPER.dll"
  Select  FileSize(wrapper_sys)
    Case  -1, -2, 0
      MessageBox_(GetFocus_(), "no VISA32_PB_WRAPPER.dll found in " + WSysName + Chr(10) + "now copy it!", "cv", #MB_ICONERROR|#MB_SYSTEMMODAL)
      result  = CopyFile(wrapper_current, wrapper_sys)
      If  result
        MessageBox_(GetFocus_(), "successful copy to " + wrapper_sys, "cv", #MB_ICONINFORMATION|#MB_SYSTEMMODAL) 
      EndIf      
    Default
      ;
  EndSelect
  
  version_current.s{1024} = GetFileVersionString(wrapper_current)
  version_sys.s{1024} = GetFileVersionString(wrapper_sys)
  md5_current.s{1024} = MD5FileFingerprint(wrapper_current)
  md5_sys.s{1024} = MD5FileFingerprint(wrapper_sys)
  
  If  version_current = version_sys And md5_current = md5_sys
    result  = MessageBox_(GetFocus_(), wrapper_sys  + " : v" + version_sys + Chr(10) + wrapper_current + " : v" + version_current + Chr(10) + "MD5 and Version matchup, Upgrade or not?", "cv", #MB_DEFBUTTON2|#MB_YESNO|#MB_ICONQUESTION|#MB_SYSTEMMODAL)
  Else
    result  = MessageBox_(GetFocus_(), wrapper_sys  + " : v" + version_sys + Chr(10) + wrapper_current + " : v" + version_current + Chr(10) + "MD5 or Version dismatchup, Upgrade or not?", "cv", #MB_DEFBUTTON1|#MB_YESNO|#MB_ICONERROR|#MB_SYSTEMMODAL)
  EndIf
  
  Select  result
    Case  #IDYES
      result  = CopyFile(wrapper_current, wrapper_sys)
      If  result
        MessageBox_(GetFocus_(), "successful copy to " + wrapper_sys, "cv", #MB_ICONINFORMATION|#MB_SYSTEMMODAL)
      EndIf      
    Case  #IDNO
      ;      
  EndSelect
  

  End
  








;
; PureBUILD Build = 25 [generated by PureBUILD Plugin]
; IDE Options = PureBasic 5.00 (Windows - x86)
; CursorPosition = 45
; FirstLine = 25
; Folding = -
; EnableThread
; EnableXP
; EnableAdmin
; UseIcon = dll.ico
; Executable = Release\check_visa_wrapper_cv_dll_version.exe
; CurrentDirectory = i:\VC++6_cv\cvPXISDK\cvPXIRunTime\cvPXIRunTime\RUN-TIMES\NI-VISA\VISA32_PB_WRAPPER\BIN\Release\
; CompileSourceDirectory
; EnableCompileCount = 130
; EnableBuildCount = 25
; EnableExeConstant
; IncludeVersionInfo
; VersionField0 = 1,0,0,%BUILDCOUNT
; VersionField1 = 1,0,0,%BUILDCOUNT
; VersionField2 = cv
; VersionField3 = cv
; VersionField4 = 1,0,0,%BUILDCOUNT
; VersionField5 = 1,0,0,%BUILDCOUNT
; VersionField6 = cv
; VersionField7 = cv
; VersionField8 = cv