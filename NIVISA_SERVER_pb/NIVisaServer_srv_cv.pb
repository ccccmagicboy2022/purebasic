  IncludePath  "BIN"
  
  Global  ServiceStatus.SERVICE_STATUS
  Global  hServiceStatus.l
  Global  ServiceName.s  
  Global  Finish
  Global  process_id
  
  Declare Handler(fdwControl.l)
  Declare ServiceMain(dwArgc.l, lpszArgv.l)

  AppPath.s = ProgramFilename()
  ServiceName = "NI-VisaServer_cv"
  Dim ServiceTableEntry.SERVICE_TABLE_ENTRY(1)
  Define hSCManager.l
  Define hService.l
  
  InitNetwork()
  
  Select ProgramParameter(0)
    Case "install"    ;Install service on machine
      hSCManager = OpenSCManager_(0, 0, #SC_MANAGER_CREATE_SERVICE)
      hService = CreateService_(hSCManager, ServiceName, ServiceName, #SERVICE_ALL_ACCESS, #SERVICE_WIN32_OWN_PROCESS, #SERVICE_AUTO_START, #SERVICE_ERROR_NORMAL, AppPath, 0, 0, 0, 0, 0)
      CloseServiceHandle_(hService)
      CloseServiceHandle_(hSCManager)
      OutputDebugString_("NI-VisaServer_cv Service installed") 
      End
    Case "uninstall"  ;Remove service from machine
      hSCManager = OpenSCManager_(0, 0, #SC_MANAGER_CREATE_SERVICE)
      hService = OpenService_(hSCManager, ServiceName, #SERVICE_ALL_ACCESS)
      DeleteService_(hService)
      CloseServiceHandle_(hService)
      CloseServiceHandle_(hSCManager)
      OutputDebugString_("NI-VisaServer_cv Service uninstalled") 
      End
    Default           ;Start the service
      Finish = 0
      ServiceTableEntry(0)\lpServiceName = @ServiceName
      ServiceTableEntry(0)\lpServiceProc = @ServiceMain()
      ServiceTableEntry(1)\lpServiceName = #Null
      ServiceTableEntry(1)\lpServiceProc = #Null
      b = StartServiceCtrlDispatcher_(@ServiceTableEntry())
      OutputDebugString_("Starting NI-VisaServer_cv Service Result = " + Str(b))
      End
  EndSelect
  
  Procedure Handler(fdwControl.l)
    Define b.l
    
    Select fdwControl
      Case #SERVICE_CONTROL_STOP
        ServiceStatus\dwWin32ExitCode = 0
        ServiceStatus\dwCurrentState = #SERVICE_STOP_PENDING
        ServiceStatus\dwCheckPoint = 0
        ServiceStatus\dwWaitHint = 0
        b = SetServiceStatus_(hServiceStatus, ServiceStatus)
        
        Finish = 1
        ServiceStatus\dwCurrentState = #SERVICE_STOPPED
                        
        hWnd = FindWindow_(#Null, "nivisaserver_cv")
        
        If hWnd <> 0
            PostMessage_(hWnd, #WM_CLOSE, 0, 0)
        EndIf        
        
        Delay(2000)
        KillProgram(process_id)        
        
    EndSelect
    ;Send current status.
    b = SetServiceStatus_(hServiceStatus, ServiceStatus)
    
  EndProcedure
  
  Procedure ServiceMain(dwArgc.l, lpszArgv.l)
    Define b.l
  
    ;Set initial state
    ServiceStatus\dwServiceType = #SERVICE_WIN32_OWN_PROCESS
    ServiceStatus\dwCurrentState = #SERVICE_START_PENDING
    ServiceStatus\dwControlsAccepted = #SERVICE_ACCEPT_STOP | #SERVICE_ACCEPT_SHUTDOWN
    ServiceStatus\dwWin32ExitCode = 0
    ServiceStatus\dwServiceSpecificExitCode = 0
    ServiceStatus\dwCheckPoint = 0
    ServiceStatus\dwWaitHint = 0
    
    hServiceStatus = RegisterServiceCtrlHandler_(ServiceName, @Handler())
    
    ServiceStatus\dwCurrentState = #SERVICE_START_PENDING
    b = SetServiceStatus_(hServiceStatus, ServiceStatus)
    
    ;** Do Initialization Here    
    ServiceStatus\dwCurrentState = #SERVICE_RUNNING
    b = SetServiceStatus_(hServiceStatus, ServiceStatus)
    
    ;** Perform tasks
    name.s  = GetPathPart(ProgramFilename()) + "NIVISA_SERVER_cv.exe" 
    process_id = RunProgram(name, "", GetPathPart(ProgramFilename()), #PB_Program_Open)
    
    Repeat
      Delay(100)
    Until Finish = 1
    
  EndProcedure 

;
; PureBUILD Build = 55 [generated by PureBUILD Plugin]
; IDE Options = PureBasic 5.00 (Windows - x86)
; CursorPosition = 63
; FirstLine = 28
; Folding = -
; EnableThread
; EnableXP
; EnableAdmin
; UseIcon = BIN\NIVisaServer_svr.ico
; Executable = Release\NIVISA_SERVER_SRV_cv.exe
; EnableCompileCount = 117
; EnableBuildCount = 55
; EnableExeConstant
; IncludeVersionInfo
; VersionField0 = 1,0,0,%BUILDCOUNT
; VersionField1 = 1,0,0,%BUILDCOUNT
; VersionField2 = cv
; VersionField3 = cv
; VersionField4 = 1,0,0,%BUILDCOUNT
; VersionField5 = 1,0,0,%BUILDCOUNT
; VersionField6 = cv
; VersionField7 = cv
; VersionField8 = cv
; Watchlist = output