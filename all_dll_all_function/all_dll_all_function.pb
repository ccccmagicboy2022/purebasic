  ;- get version of specified file as string
  Procedure.s GetFileVersionString(FileSpec.s)
    dummy.l
    BufferLen.l
   
    VerBuffer.VS_FIXEDFILEINFO
    VerPointer.l
    VerBufferLen.l
   
    FileVersion.s
   
    If OpenLibrary(0, "version.dll")
      *GFI_Size = GetFunction(0, "GetFileVersionInfoSizeA")
      *GFI_Info = GetFunction(0, "GetFileVersionInfoA")
      *GFI_Value = GetFunction(0, "VerQueryValueA")
     
      If *GFI_Size And *GFI_Info And *GFI_Value
        ; get size of version information buffer
        BufferLen = CallFunctionFast(*GFI_Size, @FileSpec, @dummy)
        If BufferLen
          ; allocate buffer and get version information
          *Buffer = AllocateMemory(BufferLen)
          CallFunctionFast(*GFI_Info, @FileSpec, 0, BufferLen, *Buffer)
          ; find file version and copy into structure
          CallFunctionFast(*GFI_Value, *Buffer, @"\", @VerPointer, @VerBufferLen)
          CopyMemory(VerPointer, VerBuffer, VerBufferLen)
          ; build file version string "x.x.x.x"
          FileVersion = Str((VerBuffer\dwFileVersionMS & $FFFF0000) >> 16) + "." + Str(VerBuffer\dwFileVersionMS & $0000FFFF) + "." + Str((VerBuffer\dwFileVersionLS & $FFFF0000) >> 16) + "." + Str(VerBuffer\dwFileVersionLS & $0000FFFF)
        EndIf
      EndIf
      CloseLibrary(0)
    EndIf   
    ; return file version string
    ProcedureReturn FileVersion
  EndProcedure



  If  ProgramParameter(0) = ""
    current.s = GetCurrentDirectory()
  Else
    current = ProgramParameter(0)
  EndIf
  
  OutputDebugString_(ProgramParameter(0))

  count.q = 0
 
  dir = ExamineDirectory(#PB_Any, current, "*.dll")
  If  dir
    If  NextDirectoryEntry(dir)
      SetFileAttributes(current  + "导出函数列表.txt", #PB_FileSystem_Normal)
      DeleteFile(current  + "导出函数列表.txt")
      file  = CreateFile(#PB_Any, current + "导出函数列表.txt")       
    Else
      ;
    EndIf
    FinishDirectory(dir)
  EndIf
  
  dir = ExamineDirectory(#PB_Any, current, "*.dll")
  
  If  dir
    While NextDirectoryEntry(dir)
      If  DirectoryEntryType(dir) = #PB_DirectoryEntry_File
        dll_name.s  = DirectoryEntryName(dir)
        namespace_name.s  = UCase(ReplaceString(dll_name, ".", "_", #PB_String_NoCase, 1))
        dll = OpenLibrary(#PB_Any, current  + dll_name)
        file1  = CreateFile(#PB_Any, current + StringField(dll_name, 1, ".") + ".md5")
        dll_md5.s = MD5FileFingerprint(current + dll_name)
        WriteStringN(file1, dll_md5  + " " + dll_name)
        CloseFile(file1)
        fun = ExamineLibraryFunctions(dll)
        If  fun
          While NextLibraryFunction()
            fun_name.s  = LibraryFunctionName()
            OutputDebugString_(dll_name + "::"  + fun_name)
            WriteStringN(file, namespace_name + "::"  + fun_name + "()")
            count = count + 1          
          Wend
        EndIf
        CloseLibrary(dll)
      Else
        ;
      EndIf 
    Wend  
    FinishDirectory(dir)
  EndIf
  
  
  If  count
    WriteStringN(file, "Total count is " + Str(count) + ".")
    dir = ExamineDirectory(#PB_Any, current, "*.dll")
    If  dir
      While NextDirectoryEntry(dir)
        If  DirectoryEntryType(dir) = #PB_DirectoryEntry_File
          dll_name.s  = DirectoryEntryName(dir)
          If GetFileVersionString(current  + dll_name)  = "" 
            WriteStringN(file, dll_name + " 无法判断dll版本！")
          Else
            WriteStringN(file, dll_name + " version is : v"  + GetFileVersionString(current  + dll_name))
          EndIf
        Else
          ;
        EndIf 
      Wend  
      FinishDirectory(dir)
    EndIf    
    CloseFile(file)
    MessageBox_(#Null, "Results save to these files: " + Chr(10) + current  + "导出函数列表.txt", current, #MB_OK | #MB_ICONINFORMATION|#MB_SYSTEMMODAL)
  Else
    MessageBox_(#Null, "no dll file founded or dll depend files miss!", current, #MB_OK | #MB_ICONERROR|#MB_SYSTEMMODAL)  
  EndIf 
  
  
  
  End
  
  
  


;
; PureBUILD Build = 67 [generated by PureBUILD Plugin]
; IDE Options = PureBasic 4.60 (Windows - x86)
; CursorPosition = 51
; FirstLine = 32
; Folding = -
; EnableXP
; UseIcon = Danagerous.ico
; Executable = all_dll_all_function.exe
; CommandLine = i:\VC++6_cv\cvPXISDK\cvPXILib_ALL\cvPXILib\Release\
; EnableCompileCount = 80
; EnableBuildCount = 53
; EnableExeConstant
; IncludeVersionInfo
; VersionField0 = 1,0,0,%BUILDCOUNT
; VersionField1 = 1,0,0,%BUILDCOUNT
; VersionField2 = cv
; VersionField3 = cv
; VersionField4 = 1,0,0,%BUILDCOUNT
; VersionField5 = 1,0,0,%BUILDCOUNT